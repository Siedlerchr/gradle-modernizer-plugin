buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // Necessary to publish to Sonatype OSS
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    // Necessary to publish to Gradle Plugins Portal (disable it when publishing to Sonatype OSS)
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'java-gradle-plugin'
}

apply plugin: 'groovy'
apply plugin: 'java'

// Pick one:

// 1) Necessary to publish to Sonatype OSS
//apply from: "$rootDir/gradle/nexus-publishing.gradle"

// 2) Necessary to publish to Gradle Plugins Portal
//apply from: "$rootDir/gradle/gradle-plugin-publishing.gradle"

// 3) These 3 are necessary to publish to your local Maven repository
apply from: "$rootDir/gradle/additional-artifacts.gradle"
apply from: "$rootDir/gradle/local-publishing.gradle"
apply from: "$rootDir/gradle/signing.gradle"

// To publish, use these tasks:
// 1) Sonatype OSS: uploadArchives
// 2) Gradle Plugins Portal: publishPlugins
// 3) local Maven repository: publish

group = 'com.github.andygoossens'
version = '1.4.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.gaul:modernizer-maven-plugin:2.1.0'

    implementation gradleApi()
    implementation localGroovy()
}

ext.compatibilityVersion = '1.7'
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

jar {
    manifest {
        attributes["Implementation-Title"] = "Gradle Modernizer plugin"
        attributes["Implementation-Version"] = project.version
        attributes["Built-Date"] = new Date()
        attributes["Built-JDK"] = System.getProperty("java.version")
        attributes["Built-Gradle"] = gradle.gradleVersion
    }
}

wrapper {
    gradleVersion = '5.6.4'
    distributionType = Wrapper.DistributionType.ALL
}
